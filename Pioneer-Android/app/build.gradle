apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    compileSdkVersion androidId.compileSdkVersion
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId androidId.applicationId
        minSdkVersion androidId.minSdkVersion
        targetSdkVersion androidId.targetSdkVersion
        versionCode androidId.versionCode
        versionName androidId.versionName

        multiDexEnabled configs.multiDexEnabled

        //指定只支持中文字符
        resConfigs('zh-rCN')

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"]
            }
        }
    }

    signingConfigs {
        release {
            storeFile file('../pioneer.jks')
            storePassword "123456"
            keyAlias "pioneer"
            keyPassword "123456"
        }
        debug {
            storeFile file('../pioneer.jks')
            storePassword "123456"
            keyAlias "pioneer"
            keyPassword "123456"
        }
    }

    buildTypes {
        debug {
            minifyEnabled false //混淆模式
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            shrinkResources false // 移除无用的resource文件
            zipAlignEnabled false // Zipalign优化
            debugUri.each { k, v -> //创建公共常量
                buildConfigField("String", k, "\"" + v + "\"")
            }
        }
        release {
            minifyEnabled configs.minifyEnabled
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            shrinkResources configs.minifyEnabled
            zipAlignEnabled configs.minifyEnabled
            releaseUri.each { k, v ->
                buildConfigField("String", k, "\"" + v + "\"")
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        dataBinding = true
    }
    kapt {
        generateStubs = true
    }
    sourceSets {
        main {
            assets {
                srcDirs 'src/main/assets', 'src/main/assets/'
            }
        }
    }
    lintOptions {
        abortOnError false
    }
    packagingOptions {
        pickFirst 'META-INF/LICENSE.txt' // picks the JavaMail license file
    }

}

dependencies {
    processor.each { k, v -> annotationProcessor v }
    kapts.each { k, v -> kapt v }
    implementation project(path: ':library')

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
